defmodule Jido.Harness.RunManagerTest do
  use ExUnit.Case, async: false

  import Jido.Harness.TestHelpers

  setup context do
    journal_dir = Path.join(System.tmp_dir!(), "jido-harness-run-test-#{System.unique_integer([:positive])}")
    configure_test_provider(Map.put(context, :journal_dir, journal_dir))
    :ok
  end

  test "returns normalized results with ordered replay and one terminal event" do
    assert {:ok, run_id} =
             Jido.Harness.Run.start(:test, %{prompt: "ok", provider_session_id: "provider-session"})

    assert {:ok, result} = Jido.Harness.Run.await(run_id, 5_000)

    assert result.run_id == run_id
    assert result.provider == :test
    assert result.provider_session_id == "provider-session"
    assert result.status == :completed
    assert result.text == "fixture-ok"
    assert result.usage == %{"input_tokens" => 2, "output_tokens" => 1}
    assert Enum.count(result.events, &Jido.Harness.Event.terminal?/1) == 1

    assert {:ok, replayed} = Jido.Harness.Run.replay(run_id, limit: 100)
    assert Enum.map(replayed, & &1.sequence) == Enum.to_list(1..length(replayed))
    assert Enum.count(replayed, &Jido.Harness.Event.terminal?/1) == 1
    assert List.first(replayed).type == :run_started
    assert List.last(replayed).type == :run_completed

    assert {:ok, stream} = Jido.Harness.Run.stream(run_id, poll_interval_ms: 1)
    assert Enum.map(Enum.to_list(stream), & &1.sequence) == Enum.map(replayed, & &1.sequence)

    assert {:error, %Jido.Harness.Error{category: :validation}} =
             Jido.Harness.Run.replay(run_id, cursor: -1, limit: 100)

    assert {:error, %Jido.Harness.Error{category: :validation}} =
             Jido.Harness.Run.replay(run_id, limit: 10_001)
  end

  test "provider-emitted failures produce a normalized result error" do
    assert {:ok, result} = Jido.Harness.run(:test, "terminal-fail", await_timeout: 5_000)
    assert result.status == :failed

    assert %Jido.Harness.Error{
             category: :execution,
             provider: :test,
             run_id: run_id,
             message: "fixture terminal failure"
           } = result.error

    assert run_id == result.run_id
    assert Enum.count(result.events, &Jido.Harness.Event.terminal?/1) == 1
  end

  test "retains a bounded text tail for large results and marks truncation" do
    config = Application.fetch_env!(:jido_harness, :provider_config)
    test_config = config.test
    retention = Map.put(test_config.retention, :memory_bytes, 256)
    Application.put_env(:jido_harness, :provider_config, %{config | test: %{test_config | retention: retention}})

    assert {:ok, result} = Jido.Harness.run(:test, "large", await_timeout: 5_000)
    assert result.status == :completed
    assert result.text_truncated?
    assert byte_size(result.text) == 256
    assert String.ends_with?(String.duplicate("0123456789", 1_000), result.text)

    assert {:ok, events} = Jido.Harness.Run.replay(result.run_id, limit: 100)
    assert Enum.any?(events, &(&1.type == :output_text_final))
  end

  test "status exposes smoke readiness and lifecycle capabilities" do
    assert {:ok, status} = Jido.Harness.status(:test)
    assert status.smoke_ready
    assert Jido.Harness.ProviderStatus.ready?(status)
    assert status.capabilities.resume?
    refute status.capabilities.native_cancel?
  end

  test "emits direct run and adapter lifecycle telemetry without request data" do
    owner = self()
    handler = "run-lifecycle-#{System.unique_integer([:positive])}"

    :telemetry.attach_many(
      handler,
      [
        [:jido, :harness, :run, :start],
        [:jido, :harness, :run, :stop],
        [:jido, :harness, :adapter, :start],
        [:jido, :harness, :adapter, :stop]
      ],
      fn name, measurements, metadata, _config ->
        send(owner, {:lifecycle_telemetry, name, measurements, metadata})
      end,
      nil
    )

    on_exit(fn -> :telemetry.detach(handler) end)

    assert {:ok, run_id} = Jido.Harness.Run.start(:test, %{prompt: "secret prompt"})
    assert {:ok, %{status: :completed}} = Jido.Harness.Run.await(run_id, 5_000)

    for event <- [
          [:jido, :harness, :run, :start],
          [:jido, :harness, :adapter, :start],
          [:jido, :harness, :adapter, :stop],
          [:jido, :harness, :run, :stop]
        ] do
      assert_receive {:lifecycle_telemetry, ^event, measurements, metadata}
      assert metadata.run_id == run_id
      assert metadata.provider == :test
      refute inspect({measurements, metadata}) =~ "secret prompt"
    end
  end

  test "await timeout does not cancel a run" do
    assert {:ok, run_id} = Jido.Harness.Run.start(:test, %{prompt: "slow"})
    assert {:error, :timeout} = Jido.Harness.Run.await(run_id, 10)
    assert {:ok, %{state: :running}} = Jido.Harness.Run.info(run_id)

    [{worker, _value}] = Registry.lookup(Jido.Harness.RunRegistry, run_id)
    assert eventually(fn -> :sys.get_state(worker).waiters == %{} end)

    assert {:ok, %{status: :completed}} = Jido.Harness.Run.await(run_id, 5_000)
  end

  test "run survives its starting caller" do
    parent = self()

    {pid, monitor} =
      spawn_monitor(fn ->
        send(parent, {:started, Jido.Harness.Run.start(:test, %{prompt: "slow"})})
      end)

    assert_receive {:started, {:ok, run_id}}, 1_000
    assert_receive {:DOWN, ^monitor, :process, ^pid, :normal}, 1_000
    assert {:ok, %{status: :completed}} = Jido.Harness.Run.await(run_id, 5_000)
  end

  test "an abrupt run-worker crash stops its linked adapter task without retrying" do
    assert {:ok, run_id} = Jido.Harness.Run.start(:test, %{prompt: "wait"})
    [{worker, _value}] = Registry.lookup(Jido.Harness.RunRegistry, run_id)
    %{task: %{pid: adapter_task}} = :sys.get_state(worker)
    worker_monitor = Process.monitor(worker)
    task_monitor = Process.monitor(adapter_task)

    Process.exit(worker, :kill)

    assert_receive {:DOWN, ^worker_monitor, :process, ^worker, :killed}, 1_000
    assert_receive {:DOWN, ^task_monitor, :process, ^adapter_task, :killed}, 1_000
    assert eventually(fn -> Registry.lookup(Jido.Harness.RunRegistry, run_id) == [] end)
  end

  test "an abrupt direct-CLI run-worker crash cancels its owned process" do
    providers = Application.get_env(:jido_harness, :providers, %{})
    Application.put_env(:jido_harness, :providers, Map.put(providers, :owned_cli, Jido.Harness.OwnedCLITestAdapter))
    on_exit(fn -> Application.put_env(:jido_harness, :providers, providers) end)

    assert {:ok, run_id} = Jido.Harness.Run.start(:owned_cli, %{prompt: "wait"})
    process_id = await_owned_process(run_id)
    [{worker, _value}] = Registry.lookup(Jido.Harness.RunRegistry, run_id)
    Process.exit(worker, :kill)

    assert {:ok, %{state: :cancelled}} = Jido.Harness.Process.await(process_id, 5_000)
    assert eventually(fn -> Registry.lookup(Jido.Harness.RunRegistry, run_id) == [] end)
  end

  test "fallback cancellation stops the adapter worker and emits one terminal event" do
    assert {:ok, run_id} = Jido.Harness.Run.start(:test, %{prompt: "wait"})
    assert :ok = Jido.Harness.Run.cancel(run_id)
    assert {:ok, result} = Jido.Harness.Run.await(run_id, 5_000)
    assert result.status == :cancelled
    assert Enum.count(result.events, &Jido.Harness.Event.terminal?/1) == 1
    assert List.last(result.events).type == :run_cancelled
  end

  test "run-level runtime and idle timeouts cover adapter streams" do
    assert {:ok, runtime_id} =
             Jido.Harness.Run.start(:test, %{prompt: "wait", runtime_timeout_ms: 30})

    assert {:ok, runtime_result} = Jido.Harness.Run.await(runtime_id, 5_000)
    assert runtime_result.status == :failed
    assert %Jido.Harness.Error{category: :timeout} = runtime_result.error
    assert List.last(runtime_result.events).type == :run_failed

    assert {:ok, idle_id} =
             Jido.Harness.Run.start(:test, %{prompt: "wait", idle_timeout_ms: 30})

    assert {:ok, idle_result} = Jido.Harness.Run.await(idle_id, 5_000)
    assert idle_result.status == :failed
    assert %Jido.Harness.Error{category: :timeout} = idle_result.error
  end

  test "adapter failures and crashes become normalized failed results" do
    for prompt <- ["fail", "raise"] do
      assert {:ok, run_id} = Jido.Harness.Run.start(:test, %{prompt: prompt})
      assert {:ok, result} = Jido.Harness.Run.await(run_id, 5_000)
      assert result.status == :failed
      assert %Jido.Harness.Error{category: :execution, run_id: ^run_id} = result.error
      assert Enum.count(result.events, &Jido.Harness.Event.terminal?/1) == 1
    end
  end

  test "rejects unsupported normalized and provider-specific options before execution" do
    assert {:error, %Jido.Harness.Error{category: :validation}} =
             Jido.Harness.Run.start(:test, %{prompt: "ok", provider_options: %{unknown: true}})

    original = Application.get_env(:jido_harness, :providers)

    Application.put_env(:jido_harness, :providers, %{
      test: Jido.Harness.TestAdapter,
      limited: Jido.Harness.LimitedTestAdapter
    })

    on_exit(fn -> Application.put_env(:jido_harness, :providers, original) end)

    assert {:error, %Jido.Harness.Error{category: :validation, details: %{field: :model}}} =
             Jido.Harness.Run.start(:limited, %{prompt: "ok", model: "unsupported"})

    before_ids = Jido.Harness.Run.list() |> Enum.map(& &1.run_id) |> MapSet.new()

    assert {:error,
            %Jido.Harness.Error{
              category: :validation,
              provider: :amp,
              details: %{field: :model}
            }} = Jido.Harness.Run.start(:amp, %{prompt: "unsupported", model: "unsupported"})

    assert {:error,
            %Jido.Harness.Error{
              category: :validation,
              provider: :opencode,
              details: %{field: :approval_mode, value: :auto_edit}
            }} = Jido.Harness.Run.start(:opencode, %{prompt: "unsupported", approval_mode: :auto_edit})

    after_ids = Jido.Harness.Run.list() |> Enum.map(& &1.run_id) |> MapSet.new()
    assert after_ids == before_ids
  end

  test "returns validation errors for malformed option lists and await timeouts" do
    assert {:error, %Jido.Harness.Error{message: "options must be a keyword list"}} =
             Jido.Harness.Run.start(:test, "ok", [:invalid])

    assert {:error, %Jido.Harness.Error{message: "request must be a map or key-value list"}} =
             Jido.Harness.Run.start(:test, [:invalid])

    assert {:error, %Jido.Harness.Error{message: "await timeout must be :infinity or a non-negative integer"}} =
             Jido.Harness.Run.await("missing", -1)

    assert {:error, %Jido.Harness.Error{message: "options must be a keyword list"}} =
             Jido.Harness.Run.replay("missing", %{cursor: 0})
  end

  defp await_owned_process(run_id, attempts \\ 100)

  defp await_owned_process(_run_id, 0), do: flunk("owned CLI process did not start")

  defp await_owned_process(run_id, attempts) do
    case Enum.find(Jido.Harness.Process.list(), &(Map.get(&1.metadata, :run_id) == run_id)) do
      nil ->
        Process.sleep(10)
        await_owned_process(run_id, attempts - 1)

      info ->
        info.process_id
    end
  end

  defp eventually(function, attempts \\ 100)

  defp eventually(function, attempts) when attempts > 0 do
    if function.() do
      true
    else
      Process.sleep(10)
      eventually(function, attempts - 1)
    end
  end

  defp eventually(_function, 0), do: false
end
